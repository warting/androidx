/*
 * Copyright (C) 2024 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.SoftwareType
import androidx.build.PlatformIdentifier
import androidx.build.AndroidXConfig

plugins {
    id("AndroidXPlugin")
}

androidXMultiplatform {
  jvm()
  androidLibrary {
    namespace = "androidx.ink.nativeloader"
    optimization {
      it.consumerKeepRules.publish = true
      it.consumerKeepRules.files.add(new File("used_by_native.pgcfg"))
      it.consumerKeepRules.files.add(new File("external_referenced_by_native.pgcfg"))
    }
    androidResources.enable = true
  }

  defaultPlatform(PlatformIdentifier.JVM)

  sourceSets {
    commonMain {
      dependencies {
        api(libs.kotlinStdlib)

        implementation("androidx.annotation:annotation:1.8.0")
      }
    }

    jvmMain {
      dependsOn(commonMain)
      resources.srcDir(new File(
          AndroidXConfig.getPrebuiltsRoot(project),
          "androidx/ink/jvm"))
    }

    androidMain {
      dependsOn(commonMain)
    }
  }
}

androidComponents {
  onVariants(selector().all()) { variant ->
    variant.sources.jniLibs.addStaticSourceDirectory(
            new File(
                    AndroidXConfig.getPrebuiltsRoot(project),
                    "androidx/ink/android").absolutePath
    )
  }
}

androidx {
    name = "Ink Native Loader"
    type = SoftwareType.PUBLISHED_LIBRARY
    inceptionYear = "2024"
    description = "Native dependencies for Ink"
}
