/*
 * Copyright (C) 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */

import androidx.build.KotlinTarget
import androidx.build.LibraryType
import androidx.build.PlatformIdentifier
import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType

plugins {
    id("AndroidXPlugin")
    alias(libs.plugins.kotlinSerialization)
}

androidXMultiplatform {
    jvm()
    mac()
    linux()
    ios()
    watchos()
    tvos()
    androidLibrary {
        namespace = "androidx.datastore.preferences.core"
        optimization {
            it.consumerKeepRules.publish = true
            it.consumerKeepRules.files.add(
                    new File("src/jvmMain/resources/META-INF/proguard/androidx.datastore_datastore-preferences-core.pro")
            )
        }
        experimentalProperties["android.lint.useK2Uast"] = false
    }

    defaultPlatform(PlatformIdentifier.JVM)

    sourceSets {
        configureEach {
            languageSettings.optIn("kotlin.RequiresOptIn")
        }

        commonMain {
            dependencies {
                api(libs.kotlinStdlib)
                api(libs.okio)
                api(project(":datastore:datastore-core"))
                api(project(":datastore:datastore-core-okio"))
            }
        }
        commonTest {
            dependencies {
                implementation(libs.kotlinTestCommon)
                implementation(libs.kotlinTest)
                implementation(libs.kotlinTestAnnotationsCommon)
                implementation(libs.kotlinCoroutinesTest)
                implementation(project(":datastore:datastore-core"))
                implementation(project(":kruth:kruth"))
                implementation(project(":internal-testutils-datastore"))
            }
        }
        jvmAndroidMain {
            dependsOn(commonMain)
            dependencies {
                implementation(project(":datastore:datastore-preferences-proto"))
            }
        }
        jvmMain {
            dependsOn(jvmAndroidMain)
        }
        androidMain {
            dependsOn(jvmAndroidMain)
        }
        jvmAndroidTest {
            dependsOn(commonTest)
            dependencies {
                implementation(libs.junit)
            }
        }
        nativeMain {
            dependsOn(commonMain)
            dependencies {
                implementation(libs.kotlinSerializationCore)
                implementation(libs.kotlinSerializationProtobuf)
            }
        }
        jvmTest {
            dependsOn(jvmAndroidTest)
        }
        nativeTest {
            dependsOn(commonTest)
            dependencies {
                implementation(libs.kotlinTest)
                implementation(project(":internal-testutils-datastore"))
                implementation(project(":kruth:kruth"))
            }
        }

        targets.configureEach { target ->
            if (target.platformType == KotlinPlatformType.native) {
                target.compilations["main"].defaultSourceSet {
                    dependsOn(nativeMain)
                }
                target.compilations["test"].defaultSourceSet {
                    dependsOn(nativeTest)
                }
            }
        }
    }
}

androidx {
    name = "Preferences DataStore Core"
    type = LibraryType.PUBLISHED_LIBRARY
    inceptionYear = "2020"
    description = "Android Preferences DataStore without the Android Dependencies"
    legacyDisableKotlinStrictApiMode = true
    metalavaK2UastEnabled = false
    kotlinTarget = KotlinTarget.KOTLIN_1_9
}
