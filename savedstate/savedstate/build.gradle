/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */


import androidx.build.SoftwareType
import androidx.build.PlatformIdentifier
import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType
import org.jetbrains.kotlin.konan.target.Family

plugins {
    id("AndroidXPlugin")
    alias(libs.plugins.kotlinSerialization)
}

androidXMultiplatform {
    androidLibrary {
        namespace = "androidx.savedstate"
        optimization {
            it.consumerKeepRules.publish = true
            it.consumerKeepRules.files.add(new File("proguard-rules.pro"))
        }
        androidResources.enable = true
    }
    desktop()
    mac()
    linux()
    ios()
    watchos()
    tvos()
    mingwX64()
    js()
    wasmJs()

    defaultPlatform(PlatformIdentifier.ANDROID)

    sourceSets {
        commonMain {
            dependencies {
                api(libs.kotlinStdlib)
                api("androidx.annotation:annotation:1.9.1")
                implementation("androidx.lifecycle:lifecycle-common:2.9.2")
                api(libs.kotlinCoroutinesCore)
                api(libs.kotlinSerializationCore)
            }
        }

        commonTest {
            dependencies {
                implementation("androidx.lifecycle:lifecycle-runtime:2.9.2")
                implementation(project(":kruth:kruth"))
                implementation(libs.kotlinTest)
                implementation(libs.kotlinTestCommon)
                implementation(libs.kotlinTestAnnotationsCommon)
                implementation(libs.kotlinCoroutinesTest)
            }
        }

        jvmMain {
            dependsOn(commonMain)
        }

        jvmTest {
            dependsOn(commonTest)
        }

        androidMain {
            dependsOn(jvmMain)
            dependencies {
                api("androidx.annotation:annotation:1.8.1")
                implementation("androidx.core:core-ktx:1.13.1")
                implementation("androidx.core:core-viewtree:1.0.0")

            }
        }

        androidUnitTest {
            dependsOn(jvmTest)
            dependencies {
            }
        }

        androidInstrumentedTest {
            dependsOn(jvmTest)
            dependencies {
                implementation("androidx.lifecycle:lifecycle-runtime:2.9.2")
                implementation(libs.testExtJunit)
                implementation(libs.testCore)
                implementation(libs.testRunner)
                implementation(libs.testRules)
                implementation(libs.truth)
            }
        }

        nonAndroidMain {
            dependsOn(commonMain)
        }

        nonAndroidTest {
            dependsOn(commonTest)
        }

        desktopMain {
            dependsOn(jvmMain)
            dependsOn(nonAndroidMain)
        }

        desktopTest {
            dependsOn(jvmTest)
            dependsOn(nonAndroidTest)
        }

        nativeMain {
            dependsOn(commonMain)
            dependsOn(nonAndroidMain)
        }

        nativeTest {
            dependsOn(commonTest)
            dependsOn(nonAndroidTest)
        }

        unixMain {
            dependsOn(nativeMain)
        }

        unixTest {
            dependsOn(nativeTest)
        }

        darwinMain {
            dependsOn(unixMain)
        }

        darwinTest {
            dependsOn(unixTest)
        }

        linuxMain {
            dependsOn(unixMain)
        }

        linuxTest {
            dependsOn(unixTest)
        }

        mingwMain {
            dependsOn(nativeMain)
        }

        mingwTest {
            dependsOn(nativeTest)
        }

        webMain {
            dependsOn(nonAndroidMain)
        }

        webTest {
            dependsOn(nonAndroidTest)
            dependencies {
                implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.9.0")
                implementation("org.jetbrains.kotlinx:kotlinx-coroutines-test:1.9.0")
            }
        }

        targets.configureEach { target ->
            if (target.platformType == KotlinPlatformType.native) {
                if (target.konanTarget.family.appleFamily) {
                    target.compilations["main"].defaultSourceSet.dependsOn(darwinMain)
                    target.compilations["test"].defaultSourceSet.dependsOn(darwinTest)
                } else if (target.konanTarget.family == Family.LINUX) {
                    target.compilations["main"].defaultSourceSet.dependsOn(linuxMain)
                    target.compilations["test"].defaultSourceSet.dependsOn(linuxTest)
                } else if (target.konanTarget.family == Family.MINGW) {
                    target.compilations["main"].defaultSourceSet.dependsOn(mingwMain)
                    target.compilations["test"].defaultSourceSet.dependsOn(mingwTest)
                } else {
                    target.compilations["main"].defaultSourceSet.dependsOn(nativeMain)
                    target.compilations["test"].defaultSourceSet.dependsOn(nativeTest)
                }
            } else if (target.platformType in [KotlinPlatformType.js, KotlinPlatformType.wasm]) {
                target.compilations["main"].defaultSourceSet.dependsOn(webMain)
                target.compilations["test"].defaultSourceSet.dependsOn(webTest)
            }
        }
    }
}

dependencies {
    constraints {
        // Prevents symbols duplication with old versions of JetBrains' fork.
        // Starting with version 1.3.2, this module is published as empty artifact with dependency
        // to this androidx module.
        // TODO: update this constrain to 1.3.3 once out due to https://youtrack.jetbrains.com/issue/CMP-8765
        commonMainImplementation("org.jetbrains.androidx.savedstate:savedstate:1.3.2") {
            because "prevents symbols duplication"
        }
    }
}

androidx {
    name = "Saved State"
    type = SoftwareType.PUBLISHED_LIBRARY
    samples(project(":savedstate:savedstate-samples"))
    inceptionYear = "2018"
    description = "Android Lifecycle Saved State"
    enableRobolectric()
}
