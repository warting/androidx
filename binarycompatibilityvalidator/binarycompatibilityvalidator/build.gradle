/*
 * Copyright (C) 2024 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */

import androidx.build.SoftwareType
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile


plugins {
    id("AndroidXPlugin")
    id("kotlin")
}

configurations {
    // TODO(b/410631668): Migrate away from using internal API in binary compatibility validator
    // Until we migrate, use the configuration below. It removes need for suppressing
    // INVISIBLE_REFERENCE and INVISIBLE_MEMBER in the
    // :binarycompatibilityvalidator:binarycompatibilityvalidator module.
    // IDE does not support this configuration yet, so you will still see references to internal
    // members marked as an error
    friends {
        canBeResolved = true
        canBeConsumed = false
        transitive = false
        attributes {
            attribute(
                    Attribute.of("artifactType", String), "jar"
            )
        }
    }
    compileOnly.extendsFrom(friends)

}

dependencies {
    api(libs.kotlinStdlib)
    friends(libs.kotlinCompiler)
    implementation(libs.binaryCompatibilityValidator)
    testImplementation(libs.truth)
    testImplementation(libs.junit)
    testImplementation(libs.kotlinTest)
    testImplementation(libs.kotlinCompiler)
}

androidx {
    name = "AndroidX Binary Compatibility Validator"
    type = SoftwareType.INTERNAL_HOST_TEST_LIBRARY
    inceptionYear = "2024"
    description = "Enforce binary compatibility for klibs"
}

tasks.withType(KotlinCompile).configureEach { task ->
    task.compilerOptions {
        friendPaths.from(configurations.friends.incoming.artifactView {}.files)
    }
}
