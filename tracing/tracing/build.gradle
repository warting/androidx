/*
 * Copyright (C) 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */
import androidx.build.SoftwareType
import androidx.build.PlatformIdentifier
import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType
import org.jetbrains.kotlin.konan.target.KonanTarget

plugins {
    id("AndroidXPlugin")
}

androidXMultiplatform {
    androidLibrary {
        namespace = "androidx.tracing"
    }
    mac {
        binaries.framework {
            baseName = "AndroidXTracing"
        }
    }

    defaultPlatform(PlatformIdentifier.ANDROID)

    def signpostCompilation = createNativeCompilation("signpost") {
        configureEachTarget { nativeCompilation ->
            nativeCompilation.includes.from(
                    project.layout.projectDirectory.dir("src/appleMain/objc/include")
            )
            nativeCompilation.sources.from(
                    fileTree(project.layout.projectDirectory.dir("src/appleMain/objc"))
                            .matching { include "**/*.mm" }
            )
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                api(libs.kotlinStdlib)
            }
        }

        androidMain {
            dependsOn(commonMain)
            dependencies {
                implementation("androidx.annotation:annotation:1.8.1")
            }
        }

        androidInstrumentedTest {
            dependencies {
                implementation(libs.testExtJunit)
                implementation(libs.testCore)
                implementation(libs.testRunner)
                implementation(libs.junit)
            }
        }

        appleMain {
            dependsOn(commonMain)
        }

        targets.configureEach { target ->
            if (target.platformType == KotlinPlatformType.native) {
                if (target.konanTarget.family.appleFamily) {
                    signpostCompilation.configureTarget(target.konanTarget)
                    createCinterop(target, signpostCompilation)

                    target.compilations["main"].defaultSourceSet.dependsOn(appleMain)
                } else {
                    throw new GradleException("unknown native target ${target}")
                }
            }
        }
    }
}

androidx {
    name = "Tracing"
    type = SoftwareType.PUBLISHED_LIBRARY
    inceptionYear = "2020"
    description = "Android Tracing"
    deviceTests {
        enableAlsoRunningOnPhysicalDevices = true
    }
}
