/*
 * Copyright 2018 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */

import androidx.build.SoftwareType
import androidx.build.PlatformIdentifier
import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType
import org.jetbrains.kotlin.gradle.tasks.KotlinNativeCompile
import org.jetbrains.kotlin.konan.target.Family

plugins {
    id("AndroidXPlugin")
}

androidXMultiplatform {
    ios()
    js()
    jvm()
    linux()
    mac()
    mingwX64()
    tvos()
    wasmJs()
    watchos()

    defaultPlatform(PlatformIdentifier.JVM)

    sourceSets {
        configureEach {
            languageSettings.optIn("kotlin.RequiresOptIn")
            languageSettings.optIn("kotlin.contracts.ExperimentalContracts")
        }

        commonMain {
            dependencies {
                api(libs.kotlinStdlib)
                api("androidx.annotation:annotation:1.9.1")
            }
        }

        commonTest {
            dependencies {
                implementation(libs.kotlinTestAnnotationsCommon)
                implementation(libs.kotlinTest)
                implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.9.0")
                implementation("org.jetbrains.kotlinx:kotlinx-coroutines-test:1.9.0")
            }
        }

        nonJsMain {
            dependsOn(commonMain)
        }

        nonJsTest {
            dependsOn(commonTest)
        }

        jvmMain {
            dependsOn(nonJsMain)
        }

        jvmTest {
            dependsOn(nonJsTest)
            dependencies {
                implementation(libs.kotlinTestJunit)
                implementation(libs.truth)
                implementation(project(":internal-testutils-truth"))
            }
        }

        nonJvmMain {
            dependsOn(commonMain)
        }

        nativeMain {
            dependsOn(nonJsMain)
            dependsOn(nonJvmMain)
        }

        nativeTest {
            dependsOn(nonJsTest)
        }

        unixMain {
            dependsOn(nativeMain)
        }

        darwinMain {
            dependsOn(unixMain)
        }

        linuxMain {
            dependsOn(unixMain)
        }

        mingwMain {
            dependsOn(nativeMain)
        }

        webMain {
            dependsOn(nonJvmMain)
        }

        webTest {
            dependsOn(commonTest)
        }

        wasmJsMain {
            dependsOn(nonJsMain)
            dependsOn(webMain)
        }

        wasmJsTest {
            dependsOn(nonJsTest)
            dependsOn(webTest)
        }

        jsMain {
            dependsOn(webMain)
        }

        jsTest {
            dependsOn(webTest)
        }

        targets.configureEach { target ->
            if (target.platformType == KotlinPlatformType.native) {
                target.compilations["main"].defaultSourceSet {
                    if (target.konanTarget.family.appleFamily) {
                        dependsOn(darwinMain)
                    } else if (target.konanTarget.family == Family.LINUX) {
                        dependsOn(linuxMain)
                    } else if (target.konanTarget.family == Family.MINGW) {
                        dependsOn(mingwMain)
                    } else {
                        throw new GradleException("unknown native target ${target}")
                    }
                }

                target.compilations["test"].defaultSourceSet {
                    dependsOn(nativeTest)
                }
            }
        }

        // Workaround for https://youtrack.jetbrains.com/issue/KT-51763
        // Make sure commonization runs before any compilation task.
        tasks.withType(KotlinNativeCompile).configureEach {
            it.dependsOn(tasks.named("commonize"))
        }
    }
}

androidx {
    name = "collections"
    type = SoftwareType.PUBLISHED_LIBRARY
    inceptionYear = "2018"
    description = "Standalone efficient collections."
    // TODO: b/326456246
    optOutJSpecify = true
}
