/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */


import androidx.build.PlatformIdentifier
import androidx.build.SoftwareType
import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType

plugins {
    id("AndroidXPlugin")
    id("AndroidXComposePlugin")
}

androidXMultiplatform {
    mac()
    linux()
    ios()
    watchos()
    tvos()
    js()
    wasmJs()
    mingwX64()
    desktop()
    androidLibrary {
        compileSdk = 35
        namespace = "androidx.paging.compose"
    }

    defaultPlatform(PlatformIdentifier.ANDROID)

    sourceSets {
        commonMain {
            dependencies {
                implementation(libs.kotlinStdlib)
                api(project(":paging:paging-common"))
                api("androidx.compose.runtime:runtime:1.9.0")
            }
        }

        commonTest {
            dependencies {
                implementation(project(":internal-testutils-paging"))
            }
        }

        androidMain {
            dependsOn(commonMain)
            dependencies {
                implementation(project(":compose:ui:ui"))
            }
        }

        nonAndroidMain {
            dependsOn(commonMain)
        }

        androidInstrumentedTest {
            dependsOn(commonTest)
            dependencies {
                implementation(project(":compose:ui:ui-tooling"))
                implementation(project(":compose:test-utils"))
                implementation(project(":compose:ui:ui-test-junit4"))
                implementation(project(":compose:foundation:foundation"))
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
            }
        }

        webMain {
            dependsOn(nonAndroidMain)
        }

        webTest {
            dependsOn(commonTest)
        }

        desktopMain {
            dependsOn(nonAndroidMain)
        }

        desktopTest {
            dependsOn(commonTest)
        }

        targets.configureEach { target ->
            if (target.platformType == KotlinPlatformType.native) {
                target.compilations["main"].defaultSourceSet.dependsOn(nonAndroidMain)
            } else if (target.platformType in [KotlinPlatformType.js, KotlinPlatformType.wasm]) {
                target.compilations["main"].defaultSourceSet.dependsOn(webMain)
                target.compilations["test"].defaultSourceSet.dependsOn(webTest)
            }
        }
    }
}

androidx {
    name = "Paging-Compose"
    type = SoftwareType.PUBLISHED_LIBRARY_ONLY_USED_BY_KOTLIN_CONSUMERS
    inceptionYear = "2020"
    description = "Compose integration with Paging"
    legacyDisableKotlinStrictApiMode = true
    samples(project(":paging:paging-compose:paging-compose-samples"))
}

