/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */

import androidx.build.SoftwareType
import androidx.build.PlatformIdentifier
import com.android.build.api.dsl.KotlinMultiplatformAndroidHostTestCompilation
import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType
import org.jetbrains.kotlin.konan.target.Family

plugins {
    id("AndroidXPlugin")
    id("AndroidXComposePlugin")
}

androidXMultiplatform {
    androidLibrary {
        namespace = "androidx.compose.runtime"
        optimization {
            it.consumerKeepRules.publish = true
            it.consumerKeepRules.files.add(new File("proguard-rules.pro"))
        }
        compilations.withType(KotlinMultiplatformAndroidHostTestCompilation) {
            it.returnDefaultValues = true
        }
    }
    desktop()
    mingwX64()
    linux()
    mac()
    ios()
    tvos()
    watchos()
    js()
    wasmJs()

    defaultPlatform(PlatformIdentifier.ANDROID)

    sourceSets {
        commonMain {
            dependencies {
                implementation(libs.kotlinStdlibCommon)
                api(libs.kotlinCoroutinesCore)
                api(project(":compose:runtime:runtime-annotation"))
                implementation("androidx.collection:collection:1.5.0")
            }
        }

        commonTest {
            dependencies {
                // For KotlinWasm/Js, versions of toolchain and stdlib need to be the same (2.1):
                // https://youtrack.jetbrains.com/issue/KT-71032
                implementation(libs.kotlinTestForWasmTests)
                implementation(libs.kotlinCoroutinesTest)
                implementation(libs.kotlinReflect)
                implementation(project(":compose:runtime:runtime-test-utils"))
            }
        }

        nonEmulatorCommonMain {
            dependsOn(commonMain)
        }

        nonEmulatorCommonTest {
            dependsOn(commonTest)
        }

        nonEmulatorJvmMain {
            dependsOn(nonEmulatorCommonMain)
        }

        nonEmulatorJvmTest {
            dependsOn(nonEmulatorCommonTest)
        }

        jvmMain {
            dependsOn(nonEmulatorJvmMain)
            dependencies {
                implementation(libs.kotlinStdlib)
            }
        }

        jvmTest {
            dependsOn(commonTest)
        }

        androidMain {
            dependsOn(jvmMain)
            dependencies {
                api(libs.kotlinCoroutinesAndroid)
                api("androidx.annotation:annotation-experimental:1.4.1")
            }
        }

        androidInstrumentedTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(libs.testExtJunit)
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.espressoCore)
                implementation(libs.truth)
            }
        }

        androidUnitTest {
            dependsOn(jvmTest)
            dependsOn(nonEmulatorJvmTest)
        }

        nonAndroidMain {
            dependsOn(commonMain)
        }

        nonAndroidTest {
            dependsOn(commonTest)
        }

        desktopMain {
            dependsOn(jvmMain)
            dependsOn(nonAndroidMain)
        }

        desktopTest {
            dependsOn(jvmTest)
            dependsOn(nonEmulatorJvmTest)
            dependsOn(nonAndroidTest)
        }

        nonJvmMain {
            dependsOn(nonAndroidMain)
            dependencies {
                implementation(libs.atomicFu)
            }
        }

        nonJvmTest {
            dependsOn(nonAndroidTest)
        }

        nativeMain {
            dependsOn(nonAndroidMain)
            dependsOn(nonJvmMain)
        }

        nativeTest {
            dependsOn(nonAndroidTest)
            dependsOn(nonJvmTest)
            dependsOn(nonEmulatorCommonTest)
        }

        webMain {
            dependsOn(nonJvmMain)
        }

        webTest {
            dependsOn(nonJvmTest)
            dependsOn(nonEmulatorCommonTest)
        }

        jsMain {
            dependsOn(webMain)
            dependencies {
                // For KotlinWasm/Js, versions of toolchain and stdlib need to be the same (2.1):
                // https://youtrack.jetbrains.com/issue/KT-71032
                implementation(libs.kotlinStdlibJs)
            }
        }

        jsTest {
            dependsOn(webTest)
            dependencies {
                implementation(libs.kotlinTestJs)
            }
        }

        wasmJsMain {
            dependsOn(webMain)
            dependencies {
                // For KotlinWasm/Js, versions of toolchain and stdlib need to be the same (2.1):
                // https://youtrack.jetbrains.com/issue/KT-71032
                implementation(libs.kotlinStdlibWasm)
                implementation(libs.kotlinXw3c)
            }
        }

        wasmJsTest {
            dependsOn(webTest)
            dependencies {
                implementation(libs.kotlinTestWasm)
            }
        }

        unixMain {
            dependsOn(nativeMain)
        }

        unixTest {
            dependsOn(nativeTest)
        }

        darwinMain {
            dependsOn(unixMain)
        }

        darwinTest {
            dependsOn(unixTest)
        }

        linuxMain {
            dependsOn(unixMain)
        }

        linuxTest {
            dependsOn(unixTest)
        }

        targets.configureEach { target ->
            if (target.platformType == KotlinPlatformType.native) {
                if (target.konanTarget.family.appleFamily) {
                    target.compilations["main"].defaultSourceSet.dependsOn(darwinMain)
                    target.compilations["test"].defaultSourceSet.dependsOn(darwinTest)
                } else if (target.konanTarget.family == Family.LINUX) {
                    target.compilations["main"].defaultSourceSet.dependsOn(linuxMain)
                    target.compilations["test"].defaultSourceSet.dependsOn(linuxTest)
                } else {
                    target.compilations["main"].defaultSourceSet.dependsOn(nativeMain)
                    target.compilations["test"].defaultSourceSet.dependsOn(nativeTest)
                }
            }
        }
    }
}

dependencies {
    lintChecks(project(":compose:runtime:runtime-lint"))
    lintPublish(project(":compose:runtime:runtime-lint"))
}

androidx {
    name = "Compose Runtime"
    type = SoftwareType.PUBLISHED_LIBRARY_ONLY_USED_BY_KOTLIN_CONSUMERS
    inceptionYear = "2019"
    description = "Tree composition support for code generated by the Compose compiler plugin and corresponding public API"
    samples(project(":compose:runtime:runtime:runtime-samples"))
}
