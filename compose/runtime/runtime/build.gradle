/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */


import androidx.build.KotlinTarget
import androidx.build.SoftwareType
import androidx.build.PlatformIdentifier
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id("AndroidXPlugin")
    id("AndroidXComposePlugin")
    id("com.android.library")
}

androidXMultiplatform {
    androidTarget()
    jvmStubs(/* runTests */ true)
    linuxX64Stubs()

    defaultPlatform(PlatformIdentifier.ANDROID)

    sourceSets {
        commonMain {
            dependencies {
                implementation(libs.kotlinStdlibCommon)
                api(libs.kotlinCoroutinesCore)
                api(project(":compose:runtime:runtime-annotation"))
                implementation("androidx.collection:collection:1.5.0")
            }
        }

        commonTest {
            dependencies {
                implementation kotlin("test")
                implementation(libs.kotlinCoroutinesTest)
                implementation(libs.kotlinReflect)
                implementation(project(":compose:runtime:runtime-test-utils"))
            }
        }

        nonEmulatorCommonMain {
            dependsOn(commonMain)
        }

        nonEmulatorCommonTest {
            dependsOn(commonTest)
        }

        nonEmulatorJvmMain {
            dependsOn(nonEmulatorCommonMain)
        }

        nonEmulatorJvmTest {
            dependsOn(nonEmulatorCommonTest)
        }

        jvmMain {
            dependsOn(nonEmulatorJvmMain)
            dependencies {
                implementation(libs.kotlinStdlib)
            }
        }

        jvmTest {
            dependsOn(commonTest)
        }

        androidMain {
            dependsOn(jvmMain)
            dependencies {
                api(libs.kotlinCoroutinesAndroid)
                api("androidx.annotation:annotation-experimental:1.4.1")
            }
        }

        androidInstrumentedTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(libs.testExtJunit)
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.truth)
            }
        }

        androidUnitTest {
            dependsOn(jvmTest)
            dependsOn(nonEmulatorJvmTest)
        }

        nonAndroidMain {
            dependsOn(commonMain)
        }

        jvmStubsMain {
            dependsOn(jvmMain)
            dependsOn(nonAndroidMain)
        }

        jvmStubsTest {
            dependsOn(jvmTest)
            dependsOn(nonEmulatorJvmTest)
        }

        linuxx64StubsMain {
            dependsOn(nonAndroidMain)
        }
    }
}

dependencies {
    lintChecks(project(":compose:runtime:runtime-lint"))
    lintPublish(project(":compose:runtime:runtime-lint"))
}

android {
    defaultConfig {
        consumerProguardFiles 'proguard-rules.pro'
    }
    namespace = "androidx.compose.runtime"
}

androidx {
    name = "Compose Runtime"
    type = SoftwareType.PUBLISHED_LIBRARY_ONLY_USED_BY_KOTLIN_CONSUMERS
    //TODO(b/407640605): Target Kotlin 2. The two tasks below fail:
    // :compose:runtime:runtime:jvmStubsTest and :compose:runtime:runtime:testReleaseUnitTest
    kotlinTarget = KotlinTarget.KOTLIN_1_9
    inceptionYear = "2019"
    description = "Tree composition support for code generated by the Compose compiler plugin and corresponding public API"
    legacyDisableKotlinStrictApiMode = true
    metalavaK2UastEnabled = false
    samples(project(":compose:runtime:runtime:runtime-samples"))
}
